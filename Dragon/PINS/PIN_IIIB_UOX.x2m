! CLE-2000 procedure to treat BWR Pin as presented in :
! "OECD/NEA Burnup Credit Criticality Benchmarks
! Phase IIIB: Burnup Calculations of BWR Fuel Assemblies
! for Storage and Transport, Feb 2002, Hiroshi OKUNO, Yoshitaka NAITO* and Kenya SUYAMA

************************************************************************
*  case PIN_1 : UOX fuel, NO Gd, NO VOID: can change                   *
*  295-group JEF3.1 DRAGLIB                                            *
*  DISTRIBUTED SELF-SHIELDING                                          *
*  Author: R. Guasch, adapted from A. Hebert, L. Ghasabyan.            *
************************************************************************

! STRUCTURES and MODULES

LINKED_LIST GEOM TRACK_SS TRACK LIBRARY LIBRARY2 SYS FLUX
  StepList BURNUP EDIOBJ ;
SEQ_ASCII UOX_PIN ;
SEQ_BINARY TF_EXC ;

SEQ_ASCII FIG.ps :: FILE './PIN_UOX_FIG.ps' ;

!Files to save for post treatment using _FILENAME convention
SEQ_ASCII _BURN :: FILE './_BURN_UOX.txt' ;
SEQ_ASCII _LIBR :: FILE './_LIB_UOX.txt' ;
SEQ_ASCII _EDIT :: FILE './_EDIT_UOX.txt' ;

STRING Library := "DLIB_295" ;

! Declation of modules called
MODULE LIB: GEO: SYBILT: G2S: SALT: MAC: USS:
  ASM: FLU: DELETE: UTL: EVO: GREP:
  EDI: END: ;
PROCEDURE Mix_UOX ;

! IIIB Benchmark uses UOX fuel : A. Santamarina recs: 4 zones in fuel
INTEGER COMB01 COMB02 COMB03 COMB04 CLAD MODE :=
        1 2 3 4 5 6 ; !  DISTRIBUTED SELF-SHIELDING 

INTEGER an2d := 12 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 20.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

! Calculating the Santamarina radii : 50%/30%/15%/5% of vol
REAL RCOMB1 RCOMB2 RCOMB3 RCOMB4 ;
EVALUATE RCOMB4 := 0.529 ;
EVALUATE RCOMB1 := 0.5 SQRT RCOMB4 * ;
EVALUATE RCOMB2 := 0.8 SQRT RCOMB4 * ;
EVALUATE RCOMB3 := 0.95 SQRT RCOMB4 * ;

! Choice of Void factor in %/100 this method might not be right 
REAL voidf := 0.0 ;
! Evaluating water density based on voiding factor 
REAL H_dens O_dens ;

EVALUATE H_dens := 1.0 voidf - 4.9316E-2 * ;
EVALUATE O_dens := 1.0 voidf - 2.4658E-2 * ;

REAL Kinf1 Keff1 ;

! Defining the IIIB PIN GEOMETRY

GEOM := GEO: :: CARCEL 5
  X- REFL X+ REFL
  Y- REFL Y+ REFL
  RADIUS 0.0 <<RCOMB1>> <<RCOMB2>>
             <<RCOMB3>> <<RCOMB4>> 0.615
  MIX <<COMB01>> <<COMB02>>
      <<COMB03>> <<COMB04>> <<CLAD>> <<MODE>>
  MESHX 0.0 1.6300 MESHY 0.0 1.6300
;

!Tracking for Self shielding and MOC with SYBILT: and SALT:
TRACK_SS := SYBILT: GEOM ::
  TITLE 'PIN_1_IIIB BENCHMARK'
  MAXR 20 QUA2 20 3 DP01 MAXZ 200000 ;

UOX_PIN FIG.ps := G2S: GEOM :: DRAWNOD ;

TRACK TF_EXC := SALT: UOX_PIN ::
  EDIT 3
  ALLG
  TSPC <<an2d>> <<densur>> REND
;

! LIBRARY definitions for XS

LIBRARY := Mix_UOX :: <<Library>> <<Option>> <<H_dens>> <<O_dens>>;
(*LIBRARY := LIB: ::
  EDIT 1
  NMIX 6    !  MAXIMUM OF MATERIAL MIXTURES
  PT
  CTRA APOL ! APOLLO TYPE TRANSPORT CORRECTION
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  CALENDF 3 PT      ! CALENDF TYPE PROBABILITY TABLES

  DEPL LIB: DRAGON FIL: DLIB_295

  MIXS LIB: DRAGON FIL: DLIB_295

  MIX <<COMB01>> 900.0
    O16     = O16    4.5504E-2
    U234    = U234   1.0443E-5 1 IRSET PT 1
    U235    = U235   1.1284E-3 1 IRSET PT 1
    U236    = U236   6.9317E-6 1 IRSET PT 1 !Question about ssh of U234 and U236
    U238    = U238   2.1606E-2 1 IRSET PT 1
  MIX <<COMB02>> COMB <<COMB01>> 1.0
  MIX <<COMB03>> COMB <<COMB01>> 1.0
  MIX <<COMB04>> COMB <<COMB01>> 1.0
  
  MIX <<CLAD>> 559.0 NOEV ! Zircalloy4 = Cr+Fe+Z 
    Cr50    = Cr50    3.2975E-6
    Cr52    = Cr52    6.3588E-5
    Cr53    = Cr53    7.2104E-6
    Cr54    = Cr54    1.7948E-6
    Fe54    = Fe54    8.6728E-6
    Fe56    = Fe56    1.3614E-4
    Fe57    = Fe57    3.1442E-6
    Fe58    = Fe58    4.1843E-7
    Zr90    = Zr90    2.2114E-2 2 IRSET PT 1
    Zr91    = Zr91    4.8226E-3 2 IRSET PT 1
    Zr92    = Zr92    7.3714E-3 2 IRSET PT 1
    Zr94    = Zr94    7.4703E-3
    Zr96    = Zr96    1.2035E-3
  MIX <<MODE>> 559.0 NOEV ! Density dependent on void factor
    H1      = H1_H2O <<H_dens>>
    O16     = O16    <<O_dens>>
;*)

! SELF-SHIELDING & FLUX

LIBRARY2 := USS: LIBRARY TRACK_SS :: EDIT 1 TRAN PASS 3 GRMIN 52 ;
SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;
FLUX := FLU: SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

! Step1 before 23/01/2024

! Edition of microlib 

EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
  EDIT 5
  MICR 12 U234 U235 U236 U238 
    Pu238 Pu239 Pu240 Pu241 Pu242 
    Am241 Am243
    Np237
  TAKE MIX 1 1 1 1 0 0
  COND
  SAVE ON FUEL
;

! KEEP GOING from here next time

REAL    Norm_f2 := 26.5 ; ! POWER NORM 26.5 W/g : assembly specific power 
INTEGER istep :=  1 ;
INTEGER nstep := 45 ;
INTEGER nauto := 45 ;
INTEGER iauto :=  1 ;
INTEGER valstep valauto ;
REAL    evobeg evoend step2 stepauto ;

StepList := UTL: :: CREA 'ListBU' <<nstep>> =
    30.00     50.00     75.00    150.00    250.00
   500.00    750.00   1000.00   2000.00   2500.00
  3000.00   3500.00   4000.00   4500.00   5000.00
  5500.00   6000.00   6500.00   7000.00   7500.00
  8000.00   8500.00   9000.00   9500.00  10000.00
 11000.00  12000.00  13000.00  14000.00  15000.00
 16000.00  17000.00  18000.00  19000.00  20000.00
 22000.00  24000.00  26000.00  28000.00  30000.00
 32000.00  34000.00  36000.00  38000.00  40000.00
;

StepList := UTL: StepList :: CREA 'ListAutop' <<nauto>> =
    30.00     50.00     75.00    150.00    250.00
   500.00    750.00   1000.00   2000.00   2500.00
  3000.00   3500.00   4000.00   4500.00   5000.00
  5500.00   6000.00   6500.00   7000.00   7500.00
  8000.00   8500.00   9000.00   9500.00  10000.00
 11000.00  12000.00  13000.00  14000.00  15000.00
 16000.00  17000.00  18000.00  19000.00  20000.00
 22000.00  24000.00  26000.00  28000.00  30000.00
 32000.00  34000.00  36000.00  38000.00  40000.00
;
GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "+++ Burnup=" 0.0 " Keff=" Keff1 ;

EVALUATE evoend := 0. ;
WHILE istep nstep <= DO

  GREP: StepList :: GETVAL 'ListBU' <<istep>> >>step2<< ;

  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 Norm_f2 / ;

  ECHO "BURNUP step" istep "between" evobeg "and" evoend "day:" ;

  IF istep 1 = THEN
    BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ELSE
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ENDIF ;

  
  GREP: StepList :: GETVAL 'ListAutop' <<iauto>> >>stepauto<< ;
  EVALUATE valstep := step2 R_TO_I ;
  EVALUATE valauto := stepauto R_TO_I ;

  IF valstep valauto = THEN

    ECHO "StepAuto:" stepauto "MWj/t" ;
    ECHO "Self-shielding calculation" istep "at" evoend "DAY:" ;

    LIBRARY2 := USS: LIBRARY LIBRARY2 TRACK_SS ::
      EDIT 1 TRAN PASS 3 GRMIN 52 ;

  ENDIF ;

  IF iauto nauto < THEN
    EVALUATE iauto := iauto 1 + ;
  ENDIF ;
  
  ECHO "step2=" step2 "evoend=" evoend ;
  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;

  FLUX := FLU: FLUX SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

  EDIOBJ := EDI: EDIOBJ FLUX LIBRARY2 TRACK ::
    EDIT 5
    MICR 12 U234 U235 U236 U238
        Pu238 Pu239 Pu240 Pu241 Pu242
        Am241 Am243
        Np237
    TAKE MIX 1 1 1 1 0 0
    COND
    SAVE ON FUEL
  ;

  GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf1<<
    GETVAL 'K-EFFECTIVE ' 1 1 1 >>Keff1<< ;

  ECHO "+++ Burnup=" step2 " Keff=" Keff1 ;

  BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK :: EDIT 2
    SAVE <<evoend>> DAY POWR <<Norm_f2>> ;

  EVALUATE istep := istep 1 + ;
ENDWHILE ;

_BURN := BURNUP ;
_LIBR := LIBRARY2 ;
_EDIT := EDIOBJ ;

ECHO "PIN_1_IIIB completed" ;

END: ;
QUIT .

