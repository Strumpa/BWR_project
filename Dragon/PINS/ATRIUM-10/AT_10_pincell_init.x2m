************************************************************************
*  case PIN_AT10_1                                                     *
*  295-group JEF3.1.1 DRAGLIB                                          *
*  DISTRIBUTED SELF-SHIELDING                                          *
*  Author: R.Guasch adapted from L. Fede and M. Fran√ßois               *
************************************************************************
! PROCEDURES 

!PROCEDURE Geom_SS Geom_FLX ;
PROCEDURE Geom_SS Geom_MAV Geom_FLX Mix_Cell ;

! STRUCTURES and MODULES

LINKED_LIST GEOMSS GEOMFLX_MAV GEOMFLX_sub TRACK_SS TRACK TRACK_NXT LIBRARY LIBRARY2 SYS SYS2 FLUX
  FLUX2 StepList BURNUP EDIOBJ ;

SEQ_ASCII UOX_CELL_SS ;
SEQ_ASCII UOX_CELL_FLX ;
SEQ_BINARY TF_EXC ;
SEQ_BINARY TF_EXC_SS ;

SEQ_ASCII FIG_SS.ps :: FILE './PIN_AT10_FIG_SS.ps' ;
SEQ_ASCII FIG_FLX.ps :: FILE './PIN_AT10_FIG_FLX.ps' ;

SEQ_ASCII _BURN :: FILE './_BURN_rowland.txt' ;
SEQ_ASCII _LIBR :: FILE './_LIB_rowland.txt' ;
SEQ_ASCII _EDIT :: FILE './_EDIT_rowland.txt' ;

MODULE LIB: GEO: SYBILT: SALT: NXT: MAC: USS: G2S:
  MCCGT: ASM: FLU: DELETE: UTL: EVO: GREP:
  EDI: END: ;

INTEGER Option := 1 ; ! Different compositions can be selected

INTEGER TRACKING_SSH := 2 ; !1 = SALT:, 2 = SYBILT:, 3 = NXT:
INTEGER TRACKING_FLX := 3 ; !1 = SALT:, 2 = SYBILT:, 3 = NXT:

INTEGER SOLVE_OPT := 1 ; ! 1=PJ, 2 = MOC

REAL H_dens O_dens := 4.94546E-02 2.47298E-02 ;

INTEGER COMB0101 COMB0102 COMB0103 COMB0104 GAP CLAD MODE :=
        1 2 3 4 5 6 7 ; !  DISTRIBUTED SELF-SHIELDING 

INTEGER an2d := 18 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 35.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

INTEGER an2d_SS := 12 ;
REAL densur_SS := 20.0 ;

* polar angles for MCCGT:
INTEGER nmu := 4 ;

* size allocation for MCCGT:
INTEGER mcu := 50000 ;

REAL Rcomb1 Rcomb2 Rcomb3 Rcomb4 ;

EVALUATE Rcomb4 := 0.4435 ;
EVALUATE Rcomb1 := 0.313602 ;
EVALUATE Rcomb2 := 0.396678 ;
EVALUATE Rcomb3 := 0.43227 ;
REAL Rgap Rclad pitch := 0.4520 0.5140 1.295 ;
REAL Rmode := 0.64 ;

REAL Kinf1 Keff1 ;

! GEOMETRY

GEOMSS := Geom_SS :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>> 
    <<Rgap>> <<Rclad>> <<pitch>> ;

GEOMFLX_sub := Geom_FLX :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
    <<Rgap>> <<Rclad>> <<Rmode>> <<pitch>> ;

GEOMFLX_MAV := Geom_MAV :: <<Rcomb1>> <<Rcomb2>> <<Rcomb3>> <<Rcomb4>>
        <<Rgap>> <<Rclad>> <<pitch>> ;

! TRACKING
* ---
* SELF-SHIELDING TRACKING
* ---
IF TRACKING_SSH 1 = THEN
  UOX_CELL_SS FIG_SS.ps := G2S: GEOMSS :: ;

  TRACK_SS TF_EXC_SS := SALT: UOX_CELL_SS ::
    EDIT 0
    ALLG
    TSPC <<an2d_SS>> <<densur_SS>> REND
  ;
ENDIF ;
IF TRACKING_SSH 2 = THEN
  TRACK_SS := SYBILT: GEOMSS ::
    TITLE 'TRACKING FOR PINCELL SS'
    MAXR 50 QUA2 24 5 DP01 MAXZ 200000 
  ;
ENDIF ;
IF TRACKING_SSH 3 = THEN
  TRACK_SS TF_EXC_SS := NXT: GEOMSS ::
    EDIT 3
    ALLG
    TSPC EQW2 5 10.0 REND
  ;
ENDIF ;
* ---
* FLUX CALC TRACKING
* ---
IF TRACKING_FLX 1 = THEN

  UOX_CELL_FLX FIG_FLX.ps := G2S: GEOMFLX_MAV :: ;
  TRACK TF_EXC := SALT: UOX_CELL_FLX ::
    EDIT 1
    ALLG
    TSPC EQW2 <<an2d>> <<densur>> REND
  ;
ENDIF ;
IF TRACKING_FLX 2 = THEN
  TRACK := SYBILT: GEOMFLX_sub ::
    MAXR 50 QUA2 32 10 DP01 MAXZ 200000
  ;
ENDIF ;
IF TRACKING_FLX 3 = THEN
  TRACK TF_EXC := NXT: GEOMFLX_sub ::
    EDIT 3
    ALLG
    TSPC EQW2 <<an2d>> <<densur>> REND
  ;
ENDIF ;

IF SOLVE_OPT 2 = THEN
  * MOC tracking
  TRACK := MCCGT: TRACK TF_EXC ::
    EDIT 0
    CACB <<nmu>> AAC 80 TMT EPSI 1E-5 MCU <<mcu>>
  ;
ENDIF ;

! LIBRARY
LIBRARY := Mix_Cell :: J311_295 <<Option>> <<H_dens>> <<O_dens>> 
    <<COMB0101>> <<COMB0102>> <<COMB0103>> <<COMB0104>> <<GAP>> <<CLAD>> <<MODE>> ;

! SELF-SHIELDING & FLUX

IF TRACKING_SSH 1 = THEN
  LIBRARY2 := USS: LIBRARY TRACK_SS TF_EXC_SS ::
          EDIT 0
          PASS 2
          GRMIN 52
  ;
ENDIF ;
IF TRACKING_SSH 2 = THEN
  LIBRARY2 := USS: LIBRARY TRACK_SS ::
          EDIT 0
          PASS 2
          GRMIN 52
  ;
ENDIF ;
IF TRACKING_SSH 3 = THEN
  LIBRARY2 := USS: LIBRARY TRACK_SS TF_EXC_SS ::
          EDIT 0
          PASS 2
          GRMIN 52
  ;
ENDIF ;

IF TRACKING_FLX 2 = THEN
  IF SOLVE_OPT 1 = THEN
    SYS := ASM: LIBRARY2 TRACK :: PIJ EDIT 0 ; 
    FLUX := FLU: LIBRARY2 SYS TRACK :: 
            EDIT 1 TYPE K B1 SIGS ;
  ENDIF ;
ELSE
  IF SOLVE_OPT 1 = THEN
      SYS := ASM: LIBRARY2 TRACK TF_EXC :: PIJ EDIT 0 ; 
      FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
              EDIT 1 TYPE K ;
    ENDIF ;
ENDIF ;

IF SOLVE_OPT 2 = THEN
  SYS := ASM: LIBRARY2 TRACK TF_EXC :: ARM EDIT 0 ; 
  FLUX := FLU: LIBRARY2 SYS TRACK TF_EXC :: 
          EDIT 1 TYPE K B1 SIGS ;
ENDIF ;


GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "Option SS =" TRACKING_SSH " Option FLX = " TRACKING_FLX " Keff=" Keff1 ;




ECHO "AT10_cell_295 completed" ;

END: ;
QUIT .
