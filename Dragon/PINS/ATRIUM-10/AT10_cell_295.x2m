************************************************************************
*  case PIN_AT10_1                                                     *
*  295-group JEF3.1.1 DRAGLIB                                          *
*  DISTRIBUTED SELF-SHIELDING                                          *
*  Author: R.Guasch adapted from A. Hebert and M. Hursin               *
************************************************************************

! STRUCTURES and MODULES

LINKED_LIST GEOM TRACK_SS TRACK LIBRARY LIBRARY2 SYS FLUX
  StepList BURNUP EDIOBJ ;

SEQ_ASCII UOX_TBH ;
SEQ_BINARY TF_EXC ;

SEQ_ASCII FIG.ps :: FILE './PIN_FIG.ps' ;

SEQ_ASCII _BURN :: FILE './_BURN_rowland.txt' ;
SEQ_ASCII _LIBR :: FILE './_LIB_rowland.txt' ;
SEQ_ASCII _EDIT :: FILE './_EDIT_rowland.txt' ;

MODULE LIB: GEO: SYBILT: G2S: SALT: MAC: USS:
  ASM: FLU: DELETE: UTL: EVO: GREP:
  EDI: END: ;

INTEGER COMB0101 COMB0105 COMB0106 COMB0107 GAIN3 MODE4 GAP :=
        1 5 6 7 2 3 4 ; !  DISTRIBUTED SELF-SHIELDING 

INTEGER an2d := 12 ;  ! ANGULAR QUADRATURE PARAMETER
REAL densur := 20.0 ; ! DENSITY OF INTEGRATION LINES CM^-1

REAL RCOMB1 RCOMB5 RCOMB6 RCOMB7 ;

EVALUATE RCOMB7 := 0.4435 ;
EVALUATE RCOMB1 := 0.5 SQRT RCOMB7 * ;
EVALUATE RCOMB5 := 0.8 SQRT RCOMB7 * ;
EVALUATE RCOMB6 := 0.95 SQRT RCOMB7 * ;

REAL Kinf1 Keff1 ;

! GEOMETRY

GEOM := GEO: :: CARCEL 6
  X- REFL X+ REFL
  Y- REFL Y+ REFL
  RADIUS 0.0 <<RCOMB1>> <<RCOMB5>>
             <<RCOMB6>> <<RCOMB7>> 0.4520 0.5140
  MIX <<COMB0101>>  <<COMB0105>>
      <<COMB0106>> <<COMB0107>> <<GAP>> <<GAIN3>> <<MODE4>>
  MESHX 0.0 1.295 MESHY 0.0 1.295
;

! TRACKING

TRACK_SS := SYBILT: GEOM ::
  TITLE 'PIN_A BENCHMARK'
  MAXR 20 QUA2 20 3 DP01 MAXZ 200000 ;

UOX_TBH FIG.ps := G2S: GEOM :: DRAWNOD ;

TRACK TF_EXC := SALT: UOX_TBH ::
  EDIT 3
  ALLG
  TSPC <<an2d>> <<densur>> REND
;

! LIBRARY

LIBRARY := LIB: ::
  EDIT 0
  NMIX 7    ! MAXIMUM OF MATERIAL MIXTURES
  SUBG
  CTRA APOL ! APOLLO TYPE TRANSPORT CORRECTION
  ANIS 2
  ADED 4 NELAS N4N N2N N3N
  CALENDF 3    ! CALENDF TYPE PROBABILITY TABLES

  DEPL LIB: DRAGON FIL: J311_295

  MIXS LIB: DRAGON FIL: J311_295

  MIX <<COMB0101>> 750.0
    O16     = O16    4.66705E-02
    U234    = U234   5.15910E-06 1 
    U235    = U235   5.67035E-04 1 
    U238    = U238   2.27631E-02 1 
  MIX <<COMB0105>> COMB <<COMB0101>> 1.0
  MIX <<COMB0106>> COMB <<COMB0101>> 1.0
  MIX <<COMB0107>> COMB <<COMB0101>> 1.0
  MIX <<GAIN3>> 559.0 NOEV
   Ni60  = Ni60   1.609524e-05
   Zr90  = Zr90   3.625294e-02  2
   Zr91  = Zr91   7.905885e-03  2
   Zr92  = Zr92   1.208432e-02  2
   Zr94  = Zr94   1.224638e-02
   Zr96  = Zr96   1.972949e-03
   Fe58  = Fe58   4.465178e-07
   Sn112  = Sn112   7.760567e-06
   Sn116  = Sn116   1.163285e-04
   Cr50  = Cr50   5.473437e-06
   Sn114  = Sn114   5.280385e-06
   Sn117  = Sn117   6.144445e-05
   Sn122  = Sn122   3.704270e-05
   O17  = O17   1.949099e-07
   Sn124  = Sn124   4.632337e-05
   Cr54  = Cr54   2.979212e-06
   Ni62  = Ni62   2.230857e-06
   Cr53  = Cr53   1.196850e-05
   Sn115  = Sn115   2.720198e-06
   Ni61  = Ni61   6.996516e-07
   Ni64  = Ni64   5.680564e-07
   Cr52  = Cr52   1.055498e-04
   O16  = O16   5.116742e-04
   Sn120  = Sn120   2.606589e-04
   Ni58  = Ni58   4.178453e-05
   Fe54  = Fe54   9.254953e-06
   Fe57  = Fe57   3.355217e-06
   Fe56  = Fe56   1.452830e-04
   Sn118  = Sn118   1.937741e-04
   Sn119  = Sn119   6.872499e-05
  MIX <<MODE4>> 559.0 NOEV
    H1      = H1_H2O 4.94546E-02  
    O16     = O16    2.47298E-02
  MIX <<GAP>> 559.0 NOEV
    He4      = He4 1.50456E-04 
;

! SELF-SHIELDING & FLUX

LIBRARY2 := USS: LIBRARY TRACK_SS :: EDIT 1 TRAN PASS 3 GRMIN 52 
           MAXST 250 ;
SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;
FLUX := FLU: SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

! EDITION

EDIOBJ := EDI: FLUX LIBRARY2 TRACK ::
  EDIT 5
  MICR 12 U235 U238 Pu238 Pu239
    Pu240 Pu241 Pu242 Am241 Am243
    Cm242 Cm244 Nd148
  TAKE MIX 1 0 0 0 1 1 1 
  COND
  SAVE ON FUEL
;
! BURN UP

REAL    Norm_f2 := 30.0 ; ! POWER NORM 30.0W/g
INTEGER istep :=  1 ;
INTEGER nstep := 44 ;
INTEGER nauto := 44 ;
INTEGER iauto :=  1 ;
INTEGER valstep valauto ;
REAL    evobeg evoend step2 stepauto ;

StepList := UTL: :: CREA 'ListBU' <<nstep>> =
 100.   500.  1000.  1500.  2000.  2500.  3000.  3500.  4000.  4500.
 5000.  5500.  6000.  6500.  7000.  7500.  8000.  8500.  9000.  9500.
 10000. 10500. 11000. 11500. 12000. 12500. 13000. 13500. 14000. 14500.
 15000. 15500. 16000. 16500. 17500. 20000. 22500. 25000. 27500. 30000.
 32500. 35000. 37500. 40000.     
 ;

StepList := UTL: StepList :: CREA 'ListAutop' <<nauto>> =
 100.   500.  1000.  1500.  2000.  2500.  3000.  3500.  4000.  4500.
 5000.  5500.  6000.  6500.  7000.  7500.  8000.  8500.  9000.  9500.
 10000. 10500. 11000. 11500. 12000. 12500. 13000. 13500. 14000. 14500.
 15000. 15500. 16000. 16500. 17500. 20000. 22500. 25000. 27500. 30000.
 32500. 35000. 37500. 40000.         
 ;

GREP: FLUX :: GETVAL 'K-EFFECTIVE  ' 1 1 1 >>Keff1<< ;
ECHO "+++ Burnup=" 0.0 " Keff=" Keff1 ;

EVALUATE evoend := 0. ;
WHILE istep nstep <= DO

  GREP: StepList :: GETVAL 'ListBU' <<istep>> >>step2<< ;

  EVALUATE evobeg := evoend ;
  EVALUATE evoend := step2 Norm_f2 / ;

  ECHO "BURNUP step" istep "between" evobeg "and" evoend "day:" ;

  IF istep 1 = THEN
    BURNUP LIBRARY2 := EVO: LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ELSE
    BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK ::
      EDIT 2
      DEPL <<evobeg>> <<evoend>> DAY POWR <<Norm_f2>>
      KAPS EXTR NSAT NODI
      GLOB
    ;

  ENDIF ;

  GREP: StepList :: GETVAL 'ListAutop' <<iauto>> >>stepauto<< ;
  EVALUATE valstep := step2 R_TO_I ;
  EVALUATE valauto := stepauto R_TO_I ;

  IF valstep valauto = THEN

    ECHO "StepAuto:" stepauto "MWj/t" ;
    ECHO "Self-shielding calculation" istep "at" evoend "DAY:" ;

    LIBRARY2 := USS: LIBRARY LIBRARY2 TRACK_SS ::
      EDIT 1 TRAN PASS 3 GRMIN 52 ;

  ENDIF ;

  IF iauto nauto < THEN
    EVALUATE iauto := iauto 1 + ;
  ENDIF ;

  ECHO "step2=" step2 "evoend=" evoend ;

  SYS := DELETE: SYS ;
  SYS := ASM: LIBRARY2 TRACK TF_EXC :: EDIT 1 PIJ ;

  FLUX := FLU: FLUX SYS LIBRARY2 TRACK :: EDIT 1 TYPE K ;

  EDIOBJ := EDI: EDIOBJ FLUX LIBRARY2 TRACK ::
    EDIT 5
    MICR 12 U235 U238 Pu238 Pu239
      Pu240 Pu241 Pu242 Am241 Am243
      Cm242 Cm244 Nd148
    TAKE MIX 1 0 0 0 1 1 1
    COND
    SAVE ON FUEL
  ;

  GREP: FLUX :: GETVAL 'K-INFINITY  ' 1 1 1 >>Kinf1<<
    GETVAL 'K-EFFECTIVE ' 1 1 1 >>Keff1<< ;

  ECHO "+++ Burnup=" step2 " Keff=" Keff1 ;

  BURNUP LIBRARY2 := EVO: BURNUP LIBRARY2 FLUX TRACK :: EDIT 2
    SAVE <<evoend>> DAY POWR <<Norm_f2>> ;

  EVALUATE istep := istep 1 + ;

ENDWHILE ;

_BURN := BURNUP ;
_LIBR := LIBRARY2 ;
_EDIT := EDIOBJ ;

ECHO "AT10_cell_295 completed" ;

END: ;
QUIT .
